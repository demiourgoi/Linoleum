// https://nightlies.apache.org/flink/flink-docs-release-1.20/docs/dev/configuration/overview/
plugins {
    // Apply the scala Plugin to add support for Scala.
    id 'scala'

    // shadow plugin to produce fat JARs
    // https://gradleup.com/shadow/getting-started/
    // https://gradleup.com/shadow/configuration/
    id("com.gradleup.shadow") version "8.3.6"
    
    // Maven Publish plugin for publishing artifacts
    id 'maven-publish'
    // https://jreleaser.org/guide/latest/quick-start/gradle.html
    id 'org.jreleaser' version '1.20.0'
}

// artifact properties
group = 'io.github.demiourgoi'
version = '0.1.1-SNAPSHOT'
description = """Linoleum flink job"""

ext {
    javaVersion = '1.8'
    flinkVersion = '1.20.1'
    scalaBinaryVersion = '_2.13'
    slf4jVersion = '1.7.36'
    log4jVersion = '2.17.1'
    sscheckVersion = '0.5.2'
    grpcVersion = '1.70.0'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


// declare where to find the dependencies of your project
repositories {
    // Use ~/.m2
    mavenLocal()
    mavenCentral()
    maven {
        url "https://repository.apache.org/content/repositories/snapshots"
        mavenContent {
            snapshotsOnly()
        }
    }
}
// NOTE: We cannot use "compileOnly" or "shadow" configurations since then we could not run code
// in the IDE or with "gradle run". We also cannot exclude transitive dependencies from the
// shadowJar yet (see https://github.com/johnrengelman/shadow/issues/159).
// -> Explicitly define the // libraries we want to be included in the "flinkShadowJar" configuration!
configurations {
    flinkShadowJar // dependencies which go into the shadowJar
    // always exclude these (also from transitive dependencies) since they are provided by Flink
    flinkShadowJar.exclude group: 'org.apache.flink', module: 'force-shading'
    flinkShadowJar.exclude group: 'com.google.code.findbugs', module: 'jsr305'
    flinkShadowJar.exclude group: 'org.slf4j'
    flinkShadowJar.exclude group: 'org.apache.logging.log4j'
}

// declare the dependencies for your production and test code
dependencies {
    // Use Scala 2.13 in our library project
    implementation libs.scala.library

    // Use Scalatest for testing our library
    testImplementation libs.junit
    testImplementation libs.scalatest.v2.v13
    testImplementation libs.junit.v4.v13.v2.v13

    // Need scala-xml at test runtime
    testRuntimeOnly libs.scala.xml.v2.v13

    // This dependency is used by the application.
    implementation libs.guava
    // https://central.sonatype.com/artifact/io.github.demiourgoi/sscheck-core_2.13
    implementation ("io.github.demiourgoi:sscheck-core${scalaBinaryVersion}:${sscheckVersion}") {
        exclude group : "org.slf4j"
    }

    // NOTE: do not shade and include the dependency, instead require the user to add it
    // That way this project does distribute Maude, and there is no issue with licenses
    // That requires building Maude Java bindings locally until that is published to
    // Maven central, as it is done in ../.github/workflows/linoleum.yml
    implementation ("es.ucm.maude:bindings:3.5.0.0")

    // NOTE: if needed we can regenerate the proto code without gRPC support to remove some dependency
    // https://github.com/grpc/grpc-java/blob/master/examples/example-opentelemetry/build.gradle
    // implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    // implementation "io.grpc:grpc-stub:${grpcVersion}"
    // runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
    // implementation "javax.annotation:javax.annotation-api:1.3.2"
    // protobuf serialization:
    // https://nightlies.apache.org/flink/flink-docs-release-1.20/docs/dev/datastream/fault-tolerance/serialization/third_party_serializers/
    implementation ("com.twitter:chill-protobuf:0.10.0") {
        exclude group: "com.esotericsoftware.kryo", module: "kryo"
    }
    implementation "com.google.protobuf:protobuf-java:3.25.6"

    // https://github.com/demiourgoi/Linoleum/issues/15
    // implementation("org.apache.commons:commons-text:1.14.0")

    // --------------------------------------------------------------
    // Compile-time dependencies that should NOT be part of the
    // shadow (uber) jar and are provided in the lib folder of Flink
    // --------------------------------------------------------------
    implementation "org.apache.flink:flink-streaming-java:${flinkVersion}"
    implementation "org.apache.flink:flink-clients:${flinkVersion}"
    implementation "org.apache.flink:flink-connector-base:${flinkVersion}"
    // https://stackoverflow.com/questions/46988499/flink-webui-when-running-from-ide
    implementation "org.apache.flink:flink-runtime-web:${flinkVersion}"

    // --------------------------------------------------------------
    // Dependencies that should be part of the shadow jar, e.g.
    // connectors. These must be in the flinkShadowJar configuration!
    // --------------------------------------------------------------
    flinkShadowJar "org.apache.flink:flink-connector-kafka:3.4.0-1.20"

    // per https://nightlies.apache.org/flink/flink-docs-release-1.20/docs/connectors/datastream/mongodb/
    // there is no connector for Flink 1.20 but there is some in https://mvnrepository.com/artifact/org.apache.flink/flink-connector-mongodb/2.0.0-1.20
    // with same maven coordinates as in https://nightlies.apache.org/flink/flink-docs-release-1.19/docs/connectors/datastream/mongodb/
    flinkShadowJar "org.apache.flink:flink-connector-mongodb:2.0.0-1.20"

    testImplementation "org.apache.flink:flink-test-utils:${flinkVersion}"

    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
    runtimeOnly "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    runtimeOnly "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    // Add test dependencies here.
    // testCompile "junit:junit:4.12"
}

// make compileOnly dependencies available for tests:
sourceSets {
    main.compileClasspath += configurations.flinkShadowJar
    main.runtimeClasspath += configurations.flinkShadowJar
    test.compileClasspath += configurations.flinkShadowJar
    test.runtimeClasspath += configurations.flinkShadowJar
    javadoc.classpath += configurations.flinkShadowJar
}

jar {
    manifest {
        attributes 'Built-By': System.getProperty('user.name'),
                'Build-Jdk': System.getProperty('java.version')
    }
}

shadowJar {
    configurations = [project.configurations.flinkShadowJar]
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

javadocJar.dependsOn javadoc

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = "linoleum${scalaBinaryVersion}"
            version = project.version
            
            from components.java
            
            // Add sources and javadoc artifacts
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'linoleum'
                description = 'Linoleum'
                url = 'https://github.com/demiourgoi/Linoleum/'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'juanrh'
                        name = 'Juan Rodriguez Hortala'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com:demiourgoi/Linoleum.git'
                    developerConnection = 'scm:git:ssh://github.com/demiourgoi/Linoleum.git'
                    url = 'http://github.com/demiourgoi/Linoleum'
                }
            }
        }
    }
    
    repositories {
        mavenLocal()
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

// https://jreleaser.org/guide/latest/examples/maven/maven-central.html#_gradle
jreleaser {
    gitRootSearch = true
    release {
        github {
            skipRelease = true
            token = 'foo'
        }
    }
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}


// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}
