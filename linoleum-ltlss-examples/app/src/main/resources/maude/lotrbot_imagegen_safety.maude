***(
Image Generation Safety Module
Defines safety states and predicate for LOTR Bot image generation

Note: goal is not only an example that works, but also one that fits
      with Maude design patterns

Quick iteration as:

cd app/src/main/resources/
maude
load ~/systems/maude/latest/model-checker.maude .
load ~/git/demiourgoi/Linoleum/maude/linoleum/trace.maude .
load lotrbot_imagegen_safety.maude .
)

omod IMAGEGEN-SAFEY is
  *** FIXME: more specific name
  pr CLASS-DECLS .

  *** Variables
  vars M O : Oid .
  var Obj, S : Object .
  vars StartTimeUnixNano EndTimeUnixNano TimeSpentOnImageGenNanos T : Nat .
  var Atts : KeyEventList .
  var monitorName : String .

  --- The monitor extends the system specification to react to span events 
  --- by storing in the monitor state information to define the property specification
  --- https://maude.lcc.uma.es/maude30-manual-html/maude-manualch1.html#x4-160001.4
  --- The monitor is a bridge from events to states
  --- timeSpentOnImageGenNanos number of nanoseconds spent generating images
  --- across all spans seen
  class Monitor | timeSpentOnImageGenNanos : Nat .
  sort MonitorId .
  subsort MonitorId < Oid .
  op mon : String -> MonitorId [ctor] .
  op initConfig : String -> Configuration .
  eq initConfig(monitorName)
    = < mon(monitorName) : Monitor | timeSpentOnImageGenNanos : 0 > .
  
  *** Messages that wrap Span objects: these are LinoleumEvent
  *** in the Linoleum Scala code
  msg spanStart : Oid Object -> Msg .
  msg spanEnd : Oid Object -> Msg .

  *** Identify image generation spans based on DEVELOPER_GUIDE.md criteria
  op isImageGenerationSpan : Object -> Bool .
  eq isImageGenerationSpan(< O : Span | attributes : Atts >) = 
    occurs(["gen_ai.operation.name", "execute_tool"], Atts) and 
    occurs(["gen_ai.tool.name", "generate_image"], Atts) .
  eq isImageGenerationSpan(Obj) = false [owise] .

  *** Calculate span duration in nanoseconds
  *** NOTE: Span is a class, not a sort
  op spanDurationNanos : Object -> Nat .
  eq spanDurationNanos(< O : Span | startTimeUnixNano : StartTimeUnixNano, endTimeUnixNano : EndTimeUnixNano >) =
    sd(EndTimeUnixNano, StartTimeUnixNano) .

  crl [on-imagegen-span-end] :
    spanEnd(M, S)
    < M : Monitor | timeSpentOnImageGenNanos : TimeSpentOnImageGenNanos >
    => < M : Monitor | timeSpentOnImageGenNanos : TimeSpentOnImageGenNanos + T >
    if isImageGenerationSpan(S) /\ T := spanDurationNanos(S) .

  crl [on-non-imagegen-span-end] :
    spanEnd(M, S)
    < M : Monitor | >
    => < M : Monitor | >
    if not isImageGenerationSpan(S) .

  rl [on-span-start] :
    spanStart(M, S)
    < M : Monitor | >
    => < M : Monitor | > .
endom

mod IMAGEGEN-SAFEY-PROPS-PARAMS is
  protecting NAT .
  op maxTimeSpentOnImageGenAllowedNanos : -> Nat .
  eq maxTimeSpentOnImageGenAllowedNanos = 3000000000 .
endm

--- Following LTL Model Checking pattern
--- https://maude.lcc.uma.es/maude-manual/maude-manualch12.html#x84-17900012
--- This is the property specification
--- https://maude.lcc.uma.es/maude30-manual-html/maude-manualch1.html#x4-160001.4
mod IMAGEGEN-SAFEY-PROPS is
  protecting IMAGEGEN-SAFEY .
  protecting IMAGEGEN-SAFEY-PROPS-PARAMS .
  including SATISFACTION . 
  including CONFIGURATION .
  subsort Configuration < State .

  var M : Oid .
  var TimeSpentOnImageGenNanos : Nat .
  var C : Configuration .

  op imageGenUsageWithinLimits : -> Prop .
  eq < M : Monitor | timeSpentOnImageGenNanos : TimeSpentOnImageGenNanos > C |= imageGenUsageWithinLimits =
    TimeSpentOnImageGenNanos < maxTimeSpentOnImageGenAllowedNanos .
endm

eof


red initConfig("safety") .

rew[100] spanEnd(mon("safety"), < span("foo") : Span | name : "myName" >) 
         < mon("safety") : Monitor | timeSpentOnImageGenNanos : 0 > .

red < mon("safety") : Monitor | timeSpentOnImageGenNanos : 3000000000 > |= imageGenUsageWithinLimits .
red < mon("safety") : Monitor | timeSpentOnImageGenNanos : 1000 > |= imageGenUsageWithinLimits .


--- Load as follows, running maude from app/src/main/resources/maude
load ~/systems/maude/latest/model-checker.maude .
load ~/git/demiourgoi/Linoleum/maude/linoleum/trace.maude .
load lotrbot_imagegen_safety.maude .