***(
Image Generation Safety Module
Defines safety states and predicate for LOTR Bot image generation

Note: goal is not only an example that works, but also one that fits
      with Maude design patterns

Quick iteration as:

cd app/src/main/resources/
maude
load ~/systems/maude/latest/model-checker.maude .
load ~/git/demiourgoi/Linoleum/maude/linoleum/trace.maude .
load lotrbot_imagegen_safety.maude .
)

--- Following LTL Model Checking pattern
--- https://maude.lcc.uma.es/maude-manual/maude-manualch12.html#x84-17900012
omod IMAGEGEN-SAFEY is
  *** FIXME: more specific name
  pr CLASS-DECLS .

  *** Variables
  vars M O : Oid .
  var Obj, S : Object .
  vars StartTimeUnixNano EndTimeUnixNano TimeSpentOnImageGenNanos T : Nat .
  var Atts : KeyEventList .
  var monitorName : String .

  --- timeSpentOnImageGenNanos number of nanoseconds spent generating images
  --- across all spans seen
  class SafetyMonitor | timeSpentOnImageGenNanos : Nat .
  sort MonitorId .
  subsort MonitorId < Oid .
  op mon : String -> MonitorId [ctor] .
  op initConfig : String -> Configuration .
  eq initConfig(monitorName)
    = < mon(monitorName) : SafetyMonitor | timeSpentOnImageGenNanos : 0 > .
  
  *** Messages that wrap Span objects: these are LinoleumEvent
  *** in the Linoleum Scala code
  msg spanStart : Oid Object -> Msg .
  msg spanEnd : Oid Object -> Msg .

  *** Identify image generation spans based on DEVELOPER_GUIDE.md criteria
  op isImageGenerationSpan : Object -> Bool .
  eq isImageGenerationSpan(< O : Span | attributes : Atts >) = 
    occurs(["gen_ai.operation.name", "execute_tool"], Atts) and 
    occurs(["gen_ai.tool.name", "generate_image"], Atts) .
  eq isImageGenerationSpan(Obj) = false [owise] .

  *** Calculate span duration in nanoseconds
  *** NOTE: Span is a class, not a sort
  op spanDurationNanos : Object -> Nat .
  eq spanDurationNanos(< O : Span | startTimeUnixNano : StartTimeUnixNano, endTimeUnixNano : EndTimeUnixNano >) =
    sd(EndTimeUnixNano, StartTimeUnixNano) .

  crl [on-imagegen-span-end] :
    spanEnd(M, S)
    < M : SafetyMonitor | timeSpentOnImageGenNanos : TimeSpentOnImageGenNanos >
    => < M : SafetyMonitor | timeSpentOnImageGenNanos : TimeSpentOnImageGenNanos + T >
    if isImageGenerationSpan(S) /\ T := spanDurationNanos(S) .

  crl [on-non-imagegen-span-end] :
    spanEnd(M, S)
    < M : SafetyMonitor | >
    => < M : SafetyMonitor | >
    if not isImageGenerationSpan(S) .

  rl [on-span-start] :
    spanStart(M, S)
    < M : SafetyMonitor | >
    => < M : SafetyMonitor | > .

endom

--- Following LTL Model Checking pattern
--- https://maude.lcc.uma.es/maude-manual/maude-manualch12.html#x84-17900012
mod IMAGEGEN-SAFEY-PREDS is
  protecting IMAGEGEN-SAFEY .
  including SATISFACTION . 
  including CONFIGURATION .
  subsort Configuration < State .

  var M : Oid .
  var TimeSpentOnImageGenNanos : Nat .
  var C : Configuration .

  op imageGenUsageWithinLimits : -> Prop .
  eq < M : SafetyMonitor | timeSpentOnImageGenNanos : TimeSpentOnImageGenNanos > C |= imageGenUsageWithinLimits =
    TimeSpentOnImageGenNanos >= 3000000000 .
endm

eof


red initConfig("safey") .

rew[100] spanEnd(mon("safey"), < span("foo") : Span | name : "myName" >) 
         < mon("safey") : SafetyMonitor | timeSpentOnImageGenNanos : 0 > .

--- Load as follows, running maude from app/src/main/resources/maude
load ~/systems/maude/latest/model-checker.maude .
load ~/git/demiourgoi/Linoleum/maude/linoleum/trace.maude .
load lotrbot_imagegen_safety.maude .