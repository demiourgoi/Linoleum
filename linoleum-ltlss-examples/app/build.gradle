// https://nightlies.apache.org/flink/flink-docs-release-1.20/docs/dev/configuration/overview/
plugins {
    // Apply the scala Plugin to add support for Scala.
    id 'scala'

    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'

    // shadow plugin to produce fat JARs
    // https://gradleup.com/shadow/getting-started/
    // https://gradleup.com/shadow/configuration/
    id("com.gradleup.shadow") version "8.3.6"
}

// artifact properties
group = 'io.github.demiourgoi'
version = '0.1.1-SNAPSHOT'
description = """Linoleum LTLss examples"""

ext {
    javaVersion = '1.8'
    flinkVersion = '1.20.1'
    scalaBinaryVersion = '_2.13'
    slf4jVersion = '1.7.36'
    log4jVersion = '2.17.1'
    sscheckVersion = '0.5.2'
    grpcVersion = '1.70.0'
    linoleumVersion = '0.2.0-SNAPSHOT'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


// declare where to find the dependencies of your project
repositories {
    // Use ~/.m2
    mavenLocal()
    mavenCentral()
    maven {
        url "https://repository.apache.org/content/repositories/snapshots"
        mavenContent {
            snapshotsOnly()
        }
    }
}
// NOTE: We cannot use "compileOnly" or "shadow" configurations since then we could not run code
// in the IDE or with "gradle run". We also cannot exclude transitive dependencies from the
// shadowJar yet (see https://github.com/johnrengelman/shadow/issues/159).
// -> Explicitly define the // libraries we want to be included in the "flinkShadowJar" configuration!
configurations {
    flinkShadowJar // dependencies which go into the shadowJar
    // always exclude these (also from transitive dependencies) since they are provided by Flink
    flinkShadowJar.exclude group: 'org.apache.flink', module: 'force-shading'
    flinkShadowJar.exclude group: 'com.google.code.findbugs', module: 'jsr305'
    flinkShadowJar.exclude group: 'org.slf4j'
    flinkShadowJar.exclude group: 'org.apache.logging.log4j'
}

// declare the dependencies for your production and test code
dependencies {
    // Use Scala 2.13 in our library project
    implementation libs.scala.library

    // Use Scalatest for testing our library
    testImplementation libs.junit
    testImplementation libs.scalatest.v2.v13
    testImplementation libs.junit.v4.v13.v2.v13

    // Need scala-xml at test runtime
    testRuntimeOnly libs.scala.xml.v2.v13

    implementation "io.github.demiourgoi:linoleum${scalaBinaryVersion}:${linoleumVersion}"
    implementation ("es.ucm.maude:bindings:3.5.0.0")

    // Writing manual HTTP client, because we cannot use langchain4j or spring-ai because both need Java 17,
    // and Flink only supports Java 8 or 11 (https://nightlies.apache.org/flink/flink-docs-release-2.0-preview1/docs/dev/configuration/gradle/)
    implementation("org.apache.httpcomponents.client5:httpclient5:5.6")
    implementation libs.gson

    // This dependency is used by the application.
    implementation libs.guava
    implementation ("io.github.demiourgoi:sscheck-core${scalaBinaryVersion}:${sscheckVersion}") {
        exclude group : "org.slf4j"
    }

    // NOTE: if needed we can regenerate the proto code without gRPC support to remove some dependency
    // https://github.com/grpc/grpc-java/blob/master/examples/example-opentelemetry/build.gradle
    // implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    // implementation "io.grpc:grpc-stub:${grpcVersion}"
    // runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
    // implementation "javax.annotation:javax.annotation-api:1.3.2"
    // protobuf serialization:
    // https://nightlies.apache.org/flink/flink-docs-release-1.20/docs/dev/datastream/fault-tolerance/serialization/third_party_serializers/
    implementation ("com.twitter:chill-protobuf:0.10.0") {
        exclude group: "com.esotericsoftware.kryo", module: "kryo"
    }
    implementation "com.google.protobuf:protobuf-java:3.25.6"


    // --------------------------------------------------------------
    // Compile-time dependencies that should NOT be part of the
    // shadow (uber) jar and are provided in the lib folder of Flink
    // --------------------------------------------------------------
    implementation "org.apache.flink:flink-streaming-java:${flinkVersion}"
    implementation "org.apache.flink:flink-clients:${flinkVersion}"
    implementation "org.apache.flink:flink-connector-base:${flinkVersion}"
    // https://stackoverflow.com/questions/46988499/flink-webui-when-running-from-ide
    implementation "org.apache.flink:flink-runtime-web:${flinkVersion}"

    // --------------------------------------------------------------
    // Dependencies that should be part of the shadow jar, e.g.
    // connectors. These must be in the flinkShadowJar configuration!
    // --------------------------------------------------------------
    flinkShadowJar "org.apache.flink:flink-connector-kafka:3.4.0-1.20"

    // per https://nightlies.apache.org/flink/flink-docs-release-1.20/docs/connectors/datastream/mongodb/
    // there is no connector for Flink 1.20 but there is some in https://mvnrepository.com/artifact/org.apache.flink/flink-connector-mongodb/2.0.0-1.20
    // with same maven coordinates as in https://nightlies.apache.org/flink/flink-docs-release-1.19/docs/connectors/datastream/mongodb/
    flinkShadowJar "org.apache.flink:flink-connector-mongodb:2.0.0-1.20"

    testImplementation "org.apache.flink:flink-test-utils:${flinkVersion}"

    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
    runtimeOnly "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    runtimeOnly "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    
    // Add test dependencies here.
    testImplementation "org.mockito:mockito-core:3.12.4"
}

// make compileOnly dependencies available for tests:
sourceSets {
    main.compileClasspath += configurations.flinkShadowJar
    main.runtimeClasspath += configurations.flinkShadowJar
    test.compileClasspath += configurations.flinkShadowJar
    test.runtimeClasspath += configurations.flinkShadowJar
    javadoc.classpath += configurations.flinkShadowJar
}
run.classpath = sourceSets.main.runtimeClasspath

jar {
    manifest {
        attributes 'Built-By': System.getProperty('user.name'),
                'Build-Jdk': System.getProperty('java.version')
    }
}

shadowJar {
    configurations = [project.configurations.flinkShadowJar]
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

application {
    // Define the main class for the application.
    mainClass = "io.github.demiourgoi.linoleum.examples.Main"

    /*
    FIXME regenerate protos https://github.com/juanrh/Linoleum/issues/6
    Caused by: java.lang.UnsupportedOperationException: As of 2022/09/29 (release 21.7) makeExtensionsImmutable should not be called from protobuf gencode. If you are seeing this message, your gencode is v
ulnerable to a denial of service attack. You should regenerate your code using protobuf 25.6 or later. Use the latest version that meets your needs. However, if you understand the risks and wish to con
tinue with vulnerable gencode, you can set the system property `-Dcom.google.protobuf.use_unsafe_pre22_gencode` on the command line. See security vulnerability: https://github.com/protocolbuffers/protobuf/security/advisories/GHSA-h4h5-3hr4-j3g2
    * */
    applicationDefaultJvmArgs = ["-Dcom.google.protobuf.use_unsafe_pre22_gencode"]
}

test {
    // Disable specs2 statistics as those are broken and lead to "FileNotFoundException: target/specs2-reports/stats"
    // https://etorreborre.github.io/specs2/guide/SPECS2-3.2/org.specs2.guide.ArgumentsReference.html
    systemProperty "specs2.neverstore", true
}
