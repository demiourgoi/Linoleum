mod LUEGO-SEMANTICS is
 pr ID-HANDLING .

 vars MSV MSV' : Map{String, Value} .
 vars CTDB LCTDB : ClientTaskDB .
 vars N TIME ELAPSED MAX : Nat .
 var  TPL : TaskPairList .
 var  DBC : DBContents .
 var  CID : ClientId .
 var  TL : TaskList .
 var  TID : TaskId .
 var  R : Result .
 var  T : Task .
 var  JL : JL .

 rl [request-reaches-balancer] :
    [CID | T TL, CTDB]
    *** loadBalancer
    lapi
    log(JL, LCTDB)
    < clock : Clock | global : TIME, elapsedSinceLast : ELAPSED >
 => [CID | TL, CTDB]
    *** loadBalancer
    lapi
    request-tid(CID, T)
    log(JL $ createInitialTrace(T, TIME, ELAPSED) $
             createTraceChild(T, 1, 11, "request-reaches-balancer", TIME, ELAPSED), LCTDB)
    clock(TIME + ELAPSED, ELAPSED) .

 rl [request-reaches-db] :
    request-tid(CID, T)
    db(DBC, N)
    log(JL, LCTDB)
    < clock : Clock | global : TIME, elapsedSinceLast : ELAPSED >
 => db(addKey(DBC, taskId(N)), s(N))
    taskIdAssigned-lapi(CID, T, taskId(N))
    log(JL $ createTraceChild(T, 1, 12, "request-reaches-db", TIME, ELAPSED), (LCTDB, [T, taskId(N)]))
    clock(TIME + ELAPSED, ELAPSED)  .

 rl [taskId-reaches-balancer] :
    taskIdAssigned-lapi(CID, T, TID)
    *** loadBalancer
    lapi
    log(JL, LCTDB)
    < clock : Clock | global : TIME, elapsedSinceLast : ELAPSED >
 => *** loadBalancer
    lapi
    taskIdAssigned(CID, T, TID)
    request-worker(T, TID)
    log(JL $ createTraceChild(T, 1, 13, "taskId-reaches-balancer", TIME, ELAPSED), LCTDB)
    clock(TIME + ELAPSED, ELAPSED) .

 rl [client-taskId-assigned] :
    taskIdAssigned(CID, T, TID)
    [CID | TL, CTDB]
    log(JL, LCTDB)
    < clock : Clock | global : TIME, elapsedSinceLast : ELAPSED >
 => [CID | TL, (CTDB, [T, TID])]
    log(JL $ createTraceChild(T, 1, 14, "client-taskId-assigned", TIME, ELAPSED), LCTDB)
    clock(TIME + ELAPSED, ELAPSED) .

 rl [add-queue] :
    request-worker(T, TID)
    queue(TPL)
    log(JL, LCTDB)
    < clock : Clock | global : TIME, elapsedSinceLast : ELAPSED >
 => queue([T, TID] ; TPL)
    log(updateTime1(JL $ createTraceChild(T, 1, 15, "add-queue", TIME, ELAPSED), T, TIME + ELAPSED), LCTDB)
    clock(TIME + ELAPSED, ELAPSED) .

 rl [worker-gets-task] :
    queue(TPL ; [T, TID])
    worker(mtTPL)
    log(JL, LCTDB)
    < clock : Clock | global : TIME, elapsedSinceLast : ELAPSED >
 => queue(TPL)
    worker([T, TID])
    log(JL $ createTraceChild(T, 2, TIME, 0, "second-half")
           $ createTraceChild(T, 2, 21, "worker-gets-task", TIME, ELAPSED), LCTDB)
    clock(TIME + ELAPSED, ELAPSED) .

 rl [work-done] :
    worker([task(N), TID])
    rapi
    log(JL, (CTDB, [T, TID]))
    < clock : Clock | global : TIME, elapsedSinceLast : ELAPSED >
 => worker(mtTPL)
    rapi
    work-done(TID, result(N))
    log(JL $ createTraceChild(T, 2, 22, "work-done", TIME, ELAPSED), (CTDB, [T, TID]))
    clock(TIME + ELAPSED, ELAPSED) .

 rl [work-done-lapi] :
    work-done(TID, result(N))
    lapi
    log(JL, (CTDB, [T, TID]))
    < clock : Clock | global : TIME, elapsedSinceLast : ELAPSED >
 => lapi
    work-done-db(TID, result(N))
    log(JL $ createTraceChild(T, 2, 23, "work-done-lapi", TIME, ELAPSED), (CTDB, [T, TID]))
    clock(TIME + ELAPSED, ELAPSED) .

 rl [work-done-db] :
    work-done-db(TID, R)
    db(DBC, N)
    log(JL, (CTDB, [T, TID]))
    < clock : Clock | global : TIME, elapsedSinceLast : ELAPSED >
 => db(add(DBC, TID, R), N)
    log(updateTime2(JL $ createTraceChild(T, 2, 24, "work-done-db", TIME, ELAPSED), T, TIME + ELAPSED), (CTDB, [T, TID]))
    clock(TIME + ELAPSED, ELAPSED) .
endm

mod TEST is
 pr LUEGO-SEMANTICS .

 op init : -> Sys .
 eq init = *** Client 0,with three tasks
           *** < c(0) : Client | tasks : task(1) task(2) task(3), ctaskDB : mtCTDB >
           *** Client 1, with two tasks
           *** < c(1) : Client | tasks : task(4) task(5), ctaskDB : mtCTDB >
           *** Client 0,with one task
           < c(0) : Client | tasks : task(1), ctaskDB : mtCTDB >
           *** Client 1, with two tasks
           *** < c(1) : Client | tasks : task(4), ctaskDB : mtCTDB >
           *** Load balancer
           < lb : LoadBalancer | none >
           lapi                                     *** Luego API, initial counter for generating task IDs
           db(mtDB, 0)                              *** Database
           queue(mtTPL)                             *** Queue
           rapi                                     *** REST API
           worker(mtTPL)                            *** Worker 0
           worker(mtTPL)                            *** Worker 1
           log(mtJL, mtCTDB)                        *** Log
           *** Clock
           < clock : Clock | global : 0, elapsedSinceLast : 500 >
           .
endm

eof

set print attribute on .

rew init .

search [1] init =>! S:Sys .