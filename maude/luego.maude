load json.maude

fmod LUEGO-SYNTAX is
 pr JSON .

 sorts Client CId LoadBalancer LuegoAPI Msg Queue Worker Sys Database DBContents RESTAPI .
 sorts Task TaskId TaskPair TaskList Result ClientTaskDB TaskPairList Log Clock .
 subsort Client Worker LoadBalancer LuegoAPI Msg Database Queue RESTAPI Log Clock < Sys .
 subsort Task < TaskList .
 subsort TaskPair < TaskPairList .
 subsort TaskPair < ClientTaskDB .

 *** System specification
 op none : -> Sys [ctor] .
 op __ : Sys Sys -> Sys [ctor assoc comm id: none] .

 *** Database for clients. Maps Task to TaskId
 op mtCTDB : -> ClientTaskDB [ctor] .
 op [_,_] : Task TaskId -> TaskPair [ctor] .
 op _,_ : ClientTaskDB ClientTaskDB -> ClientTaskDB [ctor assoc comm id: mtCTDB] .

 *** Clients and IDs
 op c : Nat -> CId [ctor] .
 op [_|_,_] : CId TaskList ClientTaskDB -> Client [ctor] .

 *** Tasks, pairs, lists, and queues
 op task : Nat -> Task [ctor] .
 op taskId : Nat -> TaskId [ctor] .

 op mtTL : -> TaskList [ctor] .
 op __ : TaskList TaskList -> TaskList [ctor assoc id: mtTL] .

 op mtTPL : -> TaskPairList [ctor] .
 op _;_ : TaskPairList TaskPairList -> TaskPairList [ctor assoc id: mtTPL] .

 op queue : TaskPairList -> Queue [ctor] .

 *** Workers
 op worker : TaskPairList -> Worker [ctor] .

 *** Load balancer
 op loadBalancer : -> LoadBalancer [ctor] .

 *** Luego API
 op lapi : -> LuegoAPI [ctor] .

 *** REST API
 op rapi : -> RESTAPI [ctor] .

 *** Database
 op mtDB : -> DBContents [ctor] .
 op [_,_] : TaskId Result -> DBContents [ctor] .
 op __ : DBContents DBContents -> DBContents [ctor assoc comm id: mtDB] .
 op db : DBContents Nat -> Database [ctor] .

 var  DBC : DBContents .
 vars R R' : Result .
 var  TID : TaskId .

 op addKey : DBContents TaskId ~> DBContents .
 ceq addKey(DBC, TID) = DBC [TID, noResult]
  if not contains(DBC, TID) .

 op add : DBContents TaskId Result -> DBContents .
 eq add([TID, R] DBC, TID, R') = [TID, R'] DBC .
 eq add(DBC, TID, R') = [TID, R'] DBC [owise] .

 op contains : DBContents TaskId -> Bool .
 eq contains([TID, R] DBC, TID) = true .
 eq contains(DBC, TID) = false [owise] .

 *** Results
 op noResult : -> Result [ctor] .
 op result : Nat -> Result [ctor] .

 *** Trace
 sort JL .
 subsort JSON < JL .

 op mtJL : -> JL [ctor] .
 op _$_ : JL JL -> JL [ctor assoc id: mtJL] .

 op log : JL -> Log [ctor] .

 *** Global time, time elapsed since last step, max
 op clock : Nat Nat Nat -> Clock [ctor] .

 *** Messages
 op request-tid : CId Task -> Msg [ctor] .
 op request-worker : Task TaskId -> Msg [ctor] .
 op taskIdAssigned : CId Task TaskId -> Msg [ctor] .
 op taskIdAssigned-lapi : CId Task TaskId -> Msg [ctor] .
 op add-database : TaskId -> Msg [ctor] .
 op work-done : TaskId Result -> Msg [ctor] .
 op work-done-db : TaskId Result -> Msg [ctor] .
endfm

mod LUEGO-SEMANTICS is
 pr LUEGO-SYNTAX .
 pr COUNTER .
 pr RANDOM .

 vars N TIME ELAPSED MAX : Nat .
 var  CTDB : ClientTaskDB .
 var  TPL : TaskPairList .
 var  DBC : DBContents .
 var  TL : TaskList .
 var  TID : TaskId .
 var  R : Result .
 var  JL : JL .
 var  CID : CId .
 var  T : Task .

 rl [request-reaches-balancer] :
    [CID | T TL, CTDB]
    *** loadBalancer
    lapi
    log(JL)
    clock(TIME, ELAPSED, MAX)
 => [CID | TL, CTDB]
    *** loadBalancer
    lapi
    request-tid(CID, T)
    log(JL $ v("a" |-> v(TIME)))
    clock(TIME + ELAPSED, random(counter) rem MAX, MAX) [print TIME] .

 rl [request-reaches-db] :
    request-tid(CID, T)
    db(DBC, N)
 => db(addKey(DBC, taskId(N)), s(N))
    taskIdAssigned-lapi(CID, T, taskId(N)) .

 rl [taskId-reaches-balancer] :
    taskIdAssigned-lapi(CID, T, TID)
    *** loadBalancer
    lapi
 => *** loadBalancer
    lapi
    taskIdAssigned(CID, T, TID)
    request-worker(T, TID) .

 rl [client-taskId-assigned] :
    taskIdAssigned(CID, T, TID)
    [CID | TL, CTDB]
 => [CID | TL, (CTDB, [T, TID])] .

 rl [add-queue] :
    request-worker(T, TID)
    queue(TPL)
 => queue([T, TID] ; TPL) .

 rl [worker-gets-task] :
    queue(TPL ; [T, TID])
    worker(mtTPL)
 => queue(TPL)
    worker([T, TID]) .

 rl [work-done] :
    worker([task(N), TID])
    rapi
 => worker(mtTPL)
    rapi
    work-done(TID, result(N)) .

 rl [work-done-lapi] :
    work-done(TID, result(N))
    lapi
 => lapi
    work-done-db(TID, result(N)) .

 rl [work-done-db] :
    work-done-db(TID, R)
    db(DBC, N)
 => db(add(DBC, TID, R), N) .
endm

mod TEST is
 pr LUEGO-SEMANTICS .

 op max : -> Nat .
 eq max = 10000 .

 op init : -> Sys .
 eq init = [c(0) | task(0) task(1) task(2), mtCTDB] *** Client 0, with three tasks
           [c(1) | task(3) task(4), mtCTDB]         *** Client 1, with two tasks
           *** loadBalancer                         *** Load balancer
           lapi                                     *** Luego API, initial counter for generating task IDs
           db(mtDB, 0)                              *** Database
           queue(mtTPL)                             *** Queue
           rapi                                     *** REST API
           worker(mtTPL)                            *** Worker 0
           worker(mtTPL)                            *** Worker 1
           log(mtJL)                                *** Log
           clock(0, random(counter) rem max, max)   *** Clock
           .
endm

set print attribute on .

rew init .


eof

lapi
rapi
queue(mtTPL)
worker(mtTPL)
worker(mtTPL)
db([taskId(0), result(0)] [taskId(1), result(1)] [taskId(2), result(2)]
   [taskId(3), result(3)] [taskId(4), result(4)], 5)
[c(0) | mtTL, [task(0), taskId(0)], [task(1), taskId(1)], [task(2), taskId(2)]]
[c(1) | mtTL, [task(3), taskId(3)], [task(4), taskId(4)]]


Juan Rodríguez Hortalá
11:21
https://github.com/juanrh/Linoleum/blob/sim_file_replayer/sim_file_replayer/app/src/main/kotlin/es/ucm/fdi/demiourgoi/linoleum/tools/simreplayer/App.kt#L42
Juan Rodríguez Hortalá
11:24
https://juanrh.grafana.net/a/grafana-exploretraces-app/explore?primarySignal=full_traces&from=now-15m&to=now&var-ds=grafanacloud-traces&var-filters=nestedSetParent%7C%3C%7C0&var-metric=rate&var-groupBy=resource.service.name&var-latencyThreshold=&var-partialLatencyThreshold=&refresh=&actionView=traceList&traceId=7de453aead3c52d7100b81d97f4ea1cb&spanId=ae8c101998925301

