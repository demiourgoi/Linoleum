omod DATA-STRUCTURES is
 pr JSON-OPS .

 sort PublisherId TopicName SubscriptionName SubscriberId .
 subsort PublisherId TopicName SubscriptionName SubscriberId < Oid .

 op publisher : Nat -> PublisherId [ctor] .
 op topic : Nat -> TopicName [ctor] .
 op subscription : Nat -> SubscriptionName [ctor] .
 op subscriber : Nat -> SubscriberId [ctor] .

 sort ExpirationPolicy .
 subsort Nat < ExpirationPolicy .

 op never : -> ExpirationPolicy [ctor] .
endom

view SubscriptionName from TRIV to DATA-STRUCTURES is
 sort Elt to SubscriptionName .
endv

view SubscriberId from TRIV to DATA-STRUCTURES is
 sort Elt to SubscriberId .
endv

view Filter from TRIV to DATA-STRUCTURES is
 sort Elt to Filter .
endv

omod PUBSUB-SYNTAX is
 pr MAP{SubscriberId, Filter} .
 pr LIST{SubscriptionName} .

 class Publisher | topicName : TopicName, msgs : JL, orderingKey : String,
                   publishTime : Nat, retryPolicy : Nat .

 class Topic | subscriptions : List{SubscriptionName},
               messageStoragePolicy : JSON, labels : JSON,
               kmsKeyName : String, retentionDuration : Nat .

 class Subscription | subscribers : Map{SubscriberId, Filter} .

 class Subscriber | received : JL .

 msg to_:_ : TopicName JSON -> Msg [prec 35] .
 msg to_:_ : SubscriptionName JSON -> Msg [prec 35] .
 msg to_:_ : SubscriberId JSON -> Msg [prec 35] .
endom

omod PUBSUB-SEMANTICS is
 pr PUBSUB-SYNTAX .

 var  SF : Entry{SubscriberId, Filter} .
 var  MSF : Map{SubscriberId, Filter} .
 var  LSN : List{SubscriptionName} .
 var  SN : SubscriptionName .
 var  SID : SubscriberId .
 var  PI : PublisherId .
 var  TN : TopicName .
 var  F : Filter .
 var  J : JSON .
 var  JL : JL .

 op broadcast : List{SubscriptionName} JSON -> Configuration .
 eq broadcast(nil, J) = none .
 eq broadcast(SN LSN, J) = to SN : J
                           broadcast(LSN, J) .

 op broadcast : Map{SubscriberId, Filter} JSON -> Configuration .
 eq broadcast(empty, J) = none .
 eq broadcast((SID |-> F, MSF), J) = if evalAttributes(J, F)
                                     then to SID : J
                                     else none
                                     fi
                                     broadcast(MSF, J) .

 rl [publisher-to-topic] :
    < PI : Publisher | topicName : TN, msgs : J $ JL >
 => < PI : Publisher | msgs : JL >
    to TN : J .

 rl [topic-to-subscription] :
    to TN : J
    < TN : Topic | subscriptions : LSN >
 => < TN : Topic | >
    broadcast(LSN, J) .

 rl [subscription-to-subscriber] :
    to SN : J
    < SN : Subscription | subscribers : MSF >
 => < SN : Subscription | >
    broadcast(MSF, J) .

 rl [subscriber] :
    to SID : J
    < SID : Subscriber | received : JL >
 => < SID : Subscriber | received : JL $ J > .
endom

mod TEST is
 pr PUBSUB-SEMANTICS .

 ops msg0 msg1 msg2 msg3 : -> JSON .
 eq msg0 = v("message" |-> v(("data"        |-> v("eyJldmVudCI6ICJ1c3VhcmlvX2NyZWFkbyJ9"),
                              "messageId"   |-> v("0"),
                              "publishTime" |-> v("2025-11-19T12:34:56.000Z"),
                              "attributes"  |-> v(("event"   |-> v("user_created"),
                                                   "origin"  |-> v("api"),
                                                   "version" |-> v("1.0")
                                                   )),
                              "subscription" |-> v("projects/my-project/subscriptions/my-sub")
                             ))
            ) .
 eq msg1 = v("message" |-> v(("data"        |-> v("eyJldmVudCI6ICJ1c3VhcmlvX2NyZWFkbyJ9"),
                              "messageId"   |-> v("1"),
                              "publishTime" |-> v("2025-11-19T12:34:56.000Z"),
                              "attributes"  |-> v(("event"   |-> v("user_created"),
                                                   "origin"  |-> v("api"),
                                                   "version" |-> v("2.0")
                                                   )),
                              "subscription" |-> v("projects/my-project/subscriptions/my-sub")
                             ))
            ) .
 eq msg2 = v("message" |-> v(("data"        |-> v("eyJldmVudCI6ICJ1c3VhcmlvX2NyZWFkbyJ9"),
                              "messageId"   |-> v("2"),
                              "publishTime" |-> v("2025-11-19T12:34:56.000Z"),
                              "attributes"  |-> v(("event"   |-> v("user_created"),
                                                   "origin"  |-> v("user"),
                                                   "version" |-> v("3.0")
                                                   )),
                              "subscription" |-> v("projects/my-project/subscriptions/my-sub")
                             ))
            ) .
 eq msg3 = v("message" |-> v(("data"        |-> v("eyJldmVudCI6ICJ1c3VhcmlvX2NyZWFkbyJ9"),
                              "messageId"   |-> v("3"),
                              "publishTime" |-> v("2025-11-19T12:34:56.000Z"),
                              "attributes"  |-> v(("event"   |-> v("user_created"),
                                                   "origin"  |-> v("user"),
                                                   "version" |-> v("3.0")
                                                   )),
                              "subscription" |-> v("projects/my-project/subscriptions/my-sub")
                             ))
            ) .

 ops publisher0 publisher1 : -> Object .
 eq publisher0 = < publisher(0) : Publisher | topicName : topic(0), msgs : msg0 $ msg1 > .
 eq publisher1 = < publisher(1) : Publisher | topicName : topic(0), msgs : msg2 $ msg3 > .

 op topic : -> Object .
 eq topic = < topic(0) : Topic | subscriptions : subscription(0) subscription(1) > .

 ops filter0 filter1 filter2 : -> Filter .
 eq filter0 = "origin" = v("api") .
 eq filter1 = "origin" = v("user") .
 eq filter2 = "version" = v("1.0") .

 ops subscription0 subscription1 : -> Object .
 eq subscription0 = < subscription(0) : Subscription | subscribers : (subscriber(0) |-> filter0,
                                                                      subscriber(1) |-> filter1) > .
 eq subscription1 = < subscription(1) : Subscription | subscribers : (subscriber(2) |-> filter2) > .

 ops subscriber0 subscriber1 subscriber2 : -> Object .
 eq subscriber0 = < subscriber(0) : Subscriber | received : mtJL > .
 eq subscriber1 = < subscriber(1) : Subscriber | received : mtJL > .
 eq subscriber2 = < subscriber(2) : Subscriber | received : mtJL > .

 op init : -> Configuration .
 eq init = publisher0 publisher1
           topic
           subscription0 subscription1
           subscriber0 subscriber1 subscriber2 .
endm

rew init .

*** TODO ACK - Funcionas o petas
*** Política de reintentos - Tiempo y máximo de reintentos

eof

< publisher(0) : Publisher | topicName : topic(0), msgs : mtJL >
< publisher(1) : Publisher | topicName : topic(0), msgs : mtJL >

< topic(0) : Topic | subscriptions : (subscription(0) subscription(1)) >

< subscription(0) : Subscription | subscribers : (subscriber(0) |-> "origin" = v("api"), subscriber(1) |-> "origin" = v("user")) >
< subscription(1) : Subscription | subscribers : subscriber(2) |-> "version" = v("1.0") >

< subscriber(0) : Subscriber | received :
    (v("message" |-> v(("attributes" |-> v(("event" |-> v("user_created"), "origin" |-> v("api"), "version" |-> v("1.0"))), "data" |-> v("eyJldmVudCI6ICJ1c3VhcmlvX2NyZWFkbyJ9"), "messageId" |-> v("0"), "publishTime" |-> v(
    "2025-11-19T12:34:56.000Z"), "subscription" |-> v("projects/my-project/subscriptions/my-sub")))) $ v("message" |-> v(("attributes" |-> v(("event" |-> v("user_created"), "origin" |-> v("api"), "version" |-> v("2.0"))), "data" |-> v(
    "eyJldmVudCI6ICJ1c3VhcmlvX2NyZWFkbyJ9"), "messageId" |-> v("1"), "publishTime" |-> v("2025-11-19T12:34:56.000Z"), "subscription" |-> v("projects/my-project/subscriptions/my-sub"))))) >

< subscriber(1) : Subscriber | received : (v("message" |->
    v(("attributes" |-> v(("event" |-> v("user_created"), "origin" |-> v("user"), "version" |-> v("3.0"))), "data" |-> v("eyJldmVudCI6ICJ1c3VhcmlvX2NyZWFkbyJ9"), "messageId" |-> v("2"), "publishTime" |-> v("2025-11-19T12:34:56.000Z"),
    "subscription" |-> v("projects/my-project/subscriptions/my-sub")))) $ v("message" |-> v(("attributes" |-> v(("event" |-> v("user_created"), "origin" |-> v("user"), "version" |-> v("3.0"))), "data" |-> v("eyJldmVudCI6ICJ1c3VhcmlvX2NyZWFkbyJ9"),
    "messageId" |-> v("3"), "publishTime" |-> v("2025-11-19T12:34:56.000Z"), "subscription" |-> v("projects/my-project/subscriptions/my-sub"))))) > < subscriber(2) : Subscriber | received : v("message" |-> v(("attributes" |-> v(("event" |-> v(
    "user_created"), "origin" |-> v("api"), "version" |-> v("1.0"))), "data" |-> v("eyJldmVudCI6ICJ1c3VhcmlvX2NyZWFkbyJ9"), "messageId" |-> v("0"), "publishTime" |-> v("2025-11-19T12:34:56.000Z"), "subscription" |-> v(
    "projects/my-project/subscriptions/my-sub")))) >

rew init .

Publisher

Función: Enviar mensajes al topic.
Atributos clave:

* topic_name	Nombre del topic al que publica.	"pedidos-nuevos"
* message	Contenido del mensaje (JSON o string).	{"id_pedido": 123, "total": 59.99}
* message_id	ID único generado por Pub/Sub.	"msg-7e3a1c"
* attributes	Metadatos en clave-valor para filtrado.	{"region": "EU", "canal": "web"}
* ordering_key (opcional)	Garantiza orden de mensajes con la misma clave.	"cliente_567"
* publish_time	Marca de tiempo de publicación.	2025-10-10T16:32:00Z
* retry_policy (opcional)	Configura reintentos automáticos.	maxRetryDuration=600s

Topic

Función: Canal intermedio que recibe los mensajes y los distribuye a suscripciones.
Atributos clave:

Atributo	Descripción	Ejemplo / Detalle
* name	Identificador único del topic.	"projects/mi-proyecto/topics/pedidos-nuevos"
* message_storage_policy	Regiones permitidas para almacenar mensajes.	{"allowedPersistenceRegions": ["europe-west1"]}
* labels	Etiquetas para organización y facturación.	{"entorno": "producción"}
* kms_key_name (opcional)	Clave KMS para cifrado del tema.	"projects/.../cryptoKeys/..."
* retention_duration	Tiempo de retención de mensajes sin confirmar.

Subscription

Función: Conexión entre un topic y un subscriber.
Atributos clave:

Atributo	Descripción	Ejemplo / Detalle
* name	Nombre único de la suscripción.	"subs-pedidos-inventario"
* topic	Nombre del topic asociado.	"pedidos-nuevos"
ack_deadline_seconds	Tiempo para confirmar (ACK) el mensaje antes del reintento.	10 – 600 s
push_config (si PUSH)	Endpoint HTTP donde Pub/Sub envía mensajes.	"https://miapi.com/webhook"
retry_policy	Retrasos entre reintentos de entrega.	minBackoff=10s, maxBackoff=600s
filter (opcional)	Solo recibe mensajes que cumplan condiciones.	"attributes.region='EU'"
dead_letter_policy (opcional)	Envía mensajes fallidos a otro topic.	"dead-letter-topic"
expiration_policy	Cuándo eliminar suscripciones inactivas.	never o 31 días

Subscriber

Función: Consume mensajes de una suscripción (por push o pull).
Atributos clave:

Atributo	Descripción	Ejemplo / Detalle
subscriber_id	Identificador del consumidor.	"svc-pagos"
mode	Modo de entrega: PUSH o PULL.	"PUSH"
endpoint (si PUSH)	URL donde recibe los mensajes.	"https://api.miempresa.com/pagos"
ack_mode	Define si reconoce automáticamente o manualmente.	"manual"
concurrency	Número de hilos o workers concurrentes.	5
error_handling	Estrategia ante fallos.	"reintentar / descartar / DLQ"
logging	Nivel de registro.	"info", "debug", etc.