fmod LUEGO-SYNTAX is
 pr JSON-OPS .
 pr CLOCK .

 sorts Client CId LoadBalancer LuegoAPI Msg Queue Worker Sys Database DBContents RESTAPI .
 sorts Task TaskId TaskPair TaskList Result ClientTaskDB TaskPairList Log .
 subsort Client Worker LoadBalancer LuegoAPI Msg Database Queue RESTAPI Log Clock < Sys .
 subsort Task < TaskList .
 subsort TaskPair < TaskPairList .
 subsort TaskPair < ClientTaskDB .

 *** System specification
 op none : -> Sys [ctor] .
 op __ : Sys Sys -> Sys [ctor assoc comm id: none] .

 *** Database for clients. Maps Task to TaskId
 op mtCTDB : -> ClientTaskDB [ctor] .
 op [_,_] : Task TaskId -> TaskPair [ctor] .
 op _,_ : ClientTaskDB ClientTaskDB -> ClientTaskDB [ctor assoc comm id: mtCTDB] .

 *** Clients and IDs
 op c : Nat -> CId [ctor] .
 op [_|_,_] : CId TaskList ClientTaskDB -> Client [ctor] .

 *** Tasks, pairs, lists, and queues
 op task : Nat -> Task [ctor] .
 op taskId : Nat -> TaskId [ctor] .

 op mtTL : -> TaskList [ctor] .
 op __ : TaskList TaskList -> TaskList [ctor assoc id: mtTL] .

 op mtTPL : -> TaskPairList [ctor] .
 op _;_ : TaskPairList TaskPairList -> TaskPairList [ctor assoc id: mtTPL] .

 op queue : TaskPairList -> Queue [ctor] .

 *** Workers
 op worker : TaskPairList -> Worker [ctor] .

 *** Load balancer
 op loadBalancer : -> LoadBalancer [ctor] .

 *** Luego API
 op lapi : -> LuegoAPI [ctor] .

 *** REST API
 op rapi : -> RESTAPI [ctor] .

 *** Database
 op mtDB : -> DBContents [ctor] .
 op [_,_] : TaskId Result -> DBContents [ctor] .
 op __ : DBContents DBContents -> DBContents [ctor assoc comm id: mtDB] .
 op db : DBContents Nat -> Database [ctor] .

 var  DBC : DBContents .
 vars R R' : Result .
 var  TID : TaskId .

 op addKey : DBContents TaskId ~> DBContents .
 ceq addKey(DBC, TID) = DBC [TID, noResult]
  if not contains(DBC, TID) .

 op add : DBContents TaskId Result -> DBContents .
 eq add([TID, R] DBC, TID, R') = [TID, R'] DBC .
 eq add(DBC, TID, R') = [TID, R'] DBC [owise] .

 op contains : DBContents TaskId -> Bool .
 eq contains([TID, R] DBC, TID) = true .
 eq contains(DBC, TID) = false [owise] .

 *** Results
 op noResult : -> Result [ctor] .
 op result : Nat -> Result [ctor] .

 *** Log
 op log : JL ClientTaskDB -> Log [ctor] .

 *** Messages
 op request-tid : CId Task -> Msg [ctor] .
 op request-worker : Task TaskId -> Msg [ctor] .
 op taskIdAssigned : CId Task TaskId -> Msg [ctor] .
 op taskIdAssigned-lapi : CId Task TaskId -> Msg [ctor] .
 op add-database : TaskId -> Msg [ctor] .
 op work-done : TaskId Result -> Msg [ctor] .
 op work-done-db : TaskId Result -> Msg [ctor] .

 var CTDB : ClientTaskDB .
 var S : Sys .
 var JL : JL .

 op printTrace : Sys -> String .
 eq printTrace(S log(JL, CTDB)) = print*(JL) .
 eq printTrace(S) = "" [owise] .
endfm

fmod ID-HANDLING is
 pr LUEGO-SYNTAX .

 vars TRACE-ID SPAN-ID SPAN-NAME PARENT-ID CHILD-ID CHILD-ID' : String .
 vars START-TIME DURATION TIME N N' : Nat .
 var  MSV : Map{String, Value} .
 vars JL JL' : JL .
 var  T : Task .

 op mkTraceId : Task -> String [memo] .
 eq mkTraceId(task(N)) = "trace" + string(N, 10) .

 op mkRootId : Task -> String [memo] .
 eq mkRootId(task(N)) = "root" + string(N, 10) .

 op mkChildId : Task Nat -> String [memo] .
 eq mkChildId(task(N), N') = "child" + string(N, 10) + string(N', 10) .

 op createInitialTrace : Task Nat Nat -> JL .
 ceq createInitialTrace(T, START-TIME, DURATION) =
                                     createTrace(T, START-TIME) $
                                     createTraceChild(TRACE-ID, CHILD-ID, mkRootId(T), START-TIME) $
                                     createTraceChild(TRACE-ID, CHILD-ID', CHILD-ID, START-TIME, DURATION)
  if TRACE-ID := mkTraceId(T) /\
     CHILD-ID := mkChildId(T, 1) /\
     CHILD-ID' := mkChildId(T, 11) .

 op createTrace : Task String Nat Nat Map{String, Value} -> JSON .
 ceq createTrace(T, SPAN-ID, START-TIME, DURATION, MSV) =
        createTrace(TRACE-ID, SPAN-ID, START-TIME, DURATION, MSV)
  if TRACE-ID := mkTraceId(T) .

 op createTrace : String String Nat Nat Map{String, Value} -> JSON .
 eq createTrace(TRACE-ID, SPAN-ID, START-TIME, DURATION, MSV) =
    v(("spanId"            |-> v(("traceId" |-> v(TRACE-ID),
                                  "spanId"  |-> v(SPAN-ID))),
       "spanName"          |-> v(SPAN-ID),
       "startTimeOffsetMs" |-> v(START-TIME),
       "durationMs"        |-> v(DURATION),
       "attributes"        |-> v(MSV))) .

 op createTrace : Task String Nat -> JSON .
 ceq createTrace(T, SPAN-ID, START-TIME) =
      createTrace(TRACE-ID, SPAN-ID, START-TIME)
  if TRACE-ID := mkTraceId(T) .


 op createTrace : String String Nat -> JSON .
 eq createTrace(TRACE-ID, SPAN-ID, START-TIME) =
    v(("spanId"            |-> v(("traceId" |-> v(TRACE-ID),
                                  "spanId"  |-> v(SPAN-ID))),
       "spanName"          |-> v(SPAN-ID),
       "startTimeOffsetMs" |-> v(START-TIME),
       "durationMs"        |-> v(0),
       "attributes"        |-> v("foo" |-> v("bar")))) .

 op createTrace : Task String Nat -> JSON .
 ceq createTrace(T, SPAN-ID, START-TIME) =
      createTrace(TRACE-ID, SPAN-ID, START-TIME)
  if TRACE-ID := mkTraceId(T) .

 op createTrace : Task Nat -> JSON .
 ceq createTrace(T, START-TIME) =
      createTrace(TRACE-ID, SPAN-ID, START-TIME)
  if TRACE-ID := mkTraceId(T) /\
     SPAN-ID := mkRootId(T) .

 op createTraceChild : Task Nat Nat Nat -> JSON .
 ceq createTraceChild(T, N, START-TIME, DURATION) =
      createTraceChild(TRACE-ID, SPAN-ID, PARENT-ID, START-TIME, DURATION)
  if TRACE-ID := mkTraceId(T) /\
     PARENT-ID := mkRootId(T) /\
     SPAN-ID := mkChildId(T, N) .

 op createTraceChild : Task Nat Nat Nat Nat -> JSON .
 ceq createTraceChild(T, N, N', START-TIME, DURATION) =
      createTraceChild(TRACE-ID, SPAN-ID, PARENT-ID, START-TIME, DURATION)
  if TRACE-ID := mkTraceId(T) /\
     PARENT-ID := mkChildId(T, N) /\
     SPAN-ID := mkChildId(T, N') .

 op createTrace : String String Nat -> JSON .
 eq createTrace(TRACE-ID, SPAN-ID, START-TIME) =
    v(("spanId"            |-> v(("traceId" |-> v(TRACE-ID),
                                  "spanId"  |-> v(SPAN-ID))),
       "spanName"          |-> v(SPAN-ID),
       "startTimeOffsetMs" |-> v(START-TIME),
       "durationMs"        |-> v(0),
       "attributes"        |-> v("foo" |-> v("bar")))) .

 op createTraceChild : String String String Nat Nat -> JSON .
 eq createTraceChild(TRACE-ID, SPAN-ID, PARENT-ID, START-TIME, DURATION) =
    v(("spanId"            |-> v(("traceId" |-> v(TRACE-ID),
                                  "spanId"  |-> v(SPAN-ID))),
       "spanName"          |-> v(SPAN-ID),
       "parentId"          |-> v(PARENT-ID),
       "startTimeOffsetMs" |-> v(START-TIME),
       "durationMs"        |-> v(DURATION),
       "attributes"        |-> v("foo" |-> v("bar")))) .

 op createTraceChild : String String String Nat -> JSON .
 eq createTraceChild(TRACE-ID, SPAN-ID, PARENT-ID, START-TIME) =
    v(("spanId"            |-> v(("traceId" |-> v(TRACE-ID),
                                  "spanId"  |-> v(SPAN-ID))),
       "spanName"          |-> v(SPAN-ID),
       "parentId"          |-> v(PARENT-ID),
       "startTimeOffsetMs" |-> v(START-TIME),
       "durationMs"        |-> v(0),
       "attributes"        |-> v("foo" |-> v("bar")))) .

 op updateTime1 : JL Task Nat -> JL .
 eq updateTime1(JL, T, TIME) = updateTime1(JL, mkChildId(T, 1), TIME) .

 op updateTime1 : JL String Nat -> JL .
 eq updateTime1(JL $ v(("durationMs"        |-> v(0),
                        "spanName"          |-> v(SPAN-ID),
                        "startTimeOffsetMs" |-> v(START-TIME),
                        MSV)) $ JL', SPAN-ID, TIME) =
                JL $ v(("durationMs"        |-> v(sd(TIME, START-TIME)),
                        "spanName"          |-> v(SPAN-ID),
                        "startTimeOffsetMs" |-> v(START-TIME),
                        MSV)) $ JL' .
 eq updateTime1(JL, SPAN-ID, TIME) = JL [owise] .

 op updateTime2 : JL Task Nat -> JL .
 eq updateTime2(JL, T, TIME) = updateRoot(updateTime2(JL, mkChildId(T, 2), TIME), mkRootId(T), TIME) .

 op updateTime2 : JL String Nat -> JL .
 eq updateTime2(JL $ v(("durationMs"        |-> v(0),
                        "spanName"          |-> v(SPAN-ID),
                        "startTimeOffsetMs" |-> v(START-TIME),
                        MSV)) $ JL', SPAN-ID, TIME) =
                JL $ v(("durationMs"        |-> v(sd(TIME, START-TIME)),
                        "spanName"          |-> v(SPAN-ID),
                        "startTimeOffsetMs" |-> v(START-TIME),
                        MSV)) $ JL' .
 eq updateTime2(JL, SPAN-ID, TIME) = JL [owise] .

 op updateRoot : JL String Nat -> JL .
 eq updateRoot(JL $ v(("durationMs"        |-> v(0),
                       "spanName"          |-> v(SPAN-ID),
                       "startTimeOffsetMs" |-> v(START-TIME),
                       MSV)) $ JL', SPAN-ID, TIME) =
               JL $ v(("durationMs"        |-> v(sd(TIME, START-TIME)),
                       "spanName"          |-> v(SPAN-ID),
                       "startTimeOffsetMs" |-> v(START-TIME),
                       MSV)) $ JL' .
 eq updateRoot(JL, SPAN-ID, TIME) = JL [owise] .
endfm

mod LUEGO-SEMANTICS is
 pr ID-HANDLING .
 *** pr COUNTER .
 pr RANDOM .

 vars N TIME ELAPSED MAX counter : Nat .
 vars MSV MSV' : Map{String, Value} .
 vars CTDB LCTDB : ClientTaskDB .
 var  TPL : TaskPairList .
 var  DBC : DBContents .
 var  TL : TaskList .
 var  TID : TaskId .
 var  R : Result .
 var  CID : CId .
 var  T : Task .
 var  JL : JL .

 rl [request-reaches-balancer] :
    [CID | T TL, CTDB]
    *** loadBalancer
    lapi
    log(JL, LCTDB)
    clock(TIME, ELAPSED, MAX, counter)
 => [CID | TL, CTDB]
    *** loadBalancer
    lapi
    request-tid(CID, T)
    log(JL $ createInitialTrace(T, TIME, ELAPSED), LCTDB)
    clock(TIME + ELAPSED, (random(counter) rem MAX) + 200, MAX, s(counter)) .

 rl [request-reaches-db] :
    request-tid(CID, T)
    db(DBC, N)
    log(JL, LCTDB)
    clock(TIME, ELAPSED, MAX, counter)
 => db(addKey(DBC, taskId(N)), s(N))
    taskIdAssigned-lapi(CID, T, taskId(N))
    log(JL $ createTraceChild(T, 1, 12, TIME, ELAPSED), (LCTDB, [T, taskId(N)]))
    clock(TIME + ELAPSED, (random(counter) rem MAX) + 200, MAX, s(counter))  .

 rl [taskId-reaches-balancer] :
    taskIdAssigned-lapi(CID, T, TID)
    *** loadBalancer
    lapi
    log(JL, LCTDB)
    clock(TIME, ELAPSED, MAX, counter)
 => *** loadBalancer
    lapi
    taskIdAssigned(CID, T, TID)
    request-worker(T, TID)
    log(JL $ createTraceChild(T, 1, 13, TIME, ELAPSED), LCTDB)
    clock(TIME + ELAPSED, (random(counter) rem MAX) + 200, MAX, s(counter)) .

 rl [client-taskId-assigned] :
    taskIdAssigned(CID, T, TID)
    [CID | TL, CTDB]
    log(JL, LCTDB)
    clock(TIME, ELAPSED, MAX, counter)
 => [CID | TL, (CTDB, [T, TID])]
    log(JL $ createTraceChild(T, 1, 14, TIME, ELAPSED), LCTDB)
    clock(TIME + ELAPSED, (random(counter) rem MAX) + 200, MAX, s(counter)) .

 rl [add-queue] :
    request-worker(T, TID)
    queue(TPL)
    log(JL, LCTDB)
    clock(TIME, ELAPSED, MAX, counter)
 => queue([T, TID] ; TPL)
    log(updateTime1(JL $ createTraceChild(T, 1, 15, TIME, ELAPSED), T, TIME + ELAPSED), LCTDB)
    clock(TIME + ELAPSED, (random(counter) rem MAX) + 200, MAX, s(counter)) .

 rl [worker-gets-task] :
    queue(TPL ; [T, TID])
    worker(mtTPL)
    log(JL, LCTDB)
    clock(TIME, ELAPSED, MAX, counter)
 => queue(TPL)
    worker([T, TID])
    log(JL $ createTraceChild(T, 2, TIME, ELAPSED)
           $ createTraceChild(T, 2, 21, TIME, ELAPSED), LCTDB)
    clock(TIME + ELAPSED, (random(counter) rem MAX) + 200, MAX, s(counter)) .

 rl [work-done] :
    worker([task(N), TID])
    rapi
    log(JL, (CTDB, [T, TID]))
    clock(TIME, ELAPSED, MAX, counter)
 => worker(mtTPL)
    rapi
    work-done(TID, result(N))
    log(JL $ createTraceChild(T, 2, 22, TIME, ELAPSED), (CTDB, [T, TID]))
    clock(TIME + ELAPSED, (random(counter) rem MAX) + 200, MAX, s(counter)) .

 rl [work-done-lapi] :
    work-done(TID, result(N))
    lapi
    log(JL, (CTDB, [T, TID]))
    clock(TIME, ELAPSED, MAX, counter)
 => lapi
    work-done-db(TID, result(N))
    log(JL $ createTraceChild(T, 2, 23, TIME, ELAPSED), (CTDB, [T, TID]))
    clock(TIME + ELAPSED, (random(counter) rem MAX) + 200, MAX, s(counter)) .

 rl [work-done-db] :
    work-done-db(TID, R)
    db(DBC, N)
    log(JL, (CTDB, [T, TID]))
    clock(TIME, ELAPSED, MAX, counter)
 => db(add(DBC, TID, R), N)
    log(updateTime2(JL $ createTraceChild(T, 2, 24, TIME, ELAPSED), T, TIME + ELAPSED), (CTDB, [T, TID]))
    clock(TIME + ELAPSED, (random(counter) rem MAX) + 200, MAX, s(counter)) .
endm

mod TEST is
 pr LUEGO-SEMANTICS .

 op max : -> Nat .
 eq max = 400 .

 op init : -> Sys .
 eq init = [c(0) | task(1) task(2) task(3), mtCTDB] *** Client 0, with three tasks
           [c(1) | task(4) task(5), mtCTDB]         *** Client 1, with two tasks
           *** [c(0) | task(1), mtCTDB]             *** Client 0, with three tasks
           *** [c(1) | task(4), mtCTDB]             *** Client 1, with two tasks
           *** loadBalancer                         *** Load balancer
           lapi                                     *** Luego API, initial counter for generating task IDs
           db(mtDB, 0)                              *** Database
           queue(mtTPL)                             *** Queue
           rapi                                     *** REST API
           worker(mtTPL)                            *** Worker 0
           worker(mtTPL)                            *** Worker 1
           log(mtJL, mtCTDB)                        *** Log
           clock(0, (random(0) rem max) + 200, max, 1)      *** Clock
           .
endm

eof

rew init .